import org.gradle.kotlin.dsl.invoke

plugins {
    val kotlinVersion = "2.3.20-RC3"
    kotlin("jvm") version kotlinVersion
    kotlin("plugin.spring") version kotlinVersion

    id("com.google.devtools.ksp") version "2.3.6"
    id("org.springframework.boot") version "4.1.0-M2"
    id("io.spring.dependency-management") version "1.1.7"
    id("com.diffplug.spotless") version "8.3.0"
    id("com.github.ben-manes.versions") version "0.53.0"
    idea
}

group = "com.github.bestheroz"
version = "0.0.1"
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:4.1.0-M2")
    }
}

dependencies {
    // Kotlin
    implementation(kotlin("reflect"))
    implementation(kotlin("stdlib"))
    ksp("com.google.dagger:dagger-compiler:2.59.2")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")

    // Coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor:1.10.2")

    // Spring
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-data-r2dbc")
    implementation("org.aspectj:aspectjweaver")
    implementation("org.apache.commons:commons-lang3")

    // Database
    implementation("io.asyncer:r2dbc-mysql:1.4.1")

    // Logging and Sentry
    implementation("com.auth0:java-jwt:4.5.1")
    implementation("io.github.oshai:kotlin-logging-jvm:8.0.01")
    implementation("io.sentry:sentry-spring-boot-4:8.35.0")
    implementation("io.sentry:sentry-logback:8.35.0")

    // OpenAPI (UI includes API dependency)
    implementation("org.springdoc:springdoc-openapi-starter-webflux-ui:3.0.2")

    // Utility
    implementation("org.fusesource.jansi:jansi:2.4.2")
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll("-Xjsr305=strict")
    }
}
tasks.withType<JavaCompile> {
    options.encoding = "UTF-8"
}

tasks.bootJar {
    archiveFileName.set("demo.jar")
}

tasks.bootRun {
    jvmArgs("--enable-native-access=ALL-UNNAMED")
}

configure<com.diffplug.gradle.spotless.SpotlessExtension> {
    kotlin {
        ktlint("1.8.0").editorConfigOverride(
            mapOf(
                "ktlint_code_style" to "ktlint_official",
                "ktlint_standard_no-wildcard-imports" to "disabled",
                "ktlint_standard_max-line-length" to "disabled",
            ),
        )
    }

    kotlinGradle {
        ktlint("1.8.0")
    }
}

configurations.compileOnly {
    extendsFrom(configurations.annotationProcessor.get())
}
